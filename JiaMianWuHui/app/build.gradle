apply plugin: 'com.android.application'


enum ApiEnvEnum {
    DEV(0, "假面舞会测试"), ONLINE(1, "假面舞会"), PRE_RELEASE(2, "假面舞会预发")

    private final int value
    private final String name

    ApiEnvEnum(int value, String name) {
        this.value = value
        this.name = name
    }

    int getValue() {
        value
    }

    String getName() {
        name
    }
}

android {
    def envStr = project.hasProperty('API_MODE') ? API_MODE : "DEV"
    def appNameStr = (envStr as ApiEnvEnum).name

    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        flavorDimensions "versionCode"
        multiDexEnabled true

        buildConfigField("int", "API_MODE", "${(envStr as ApiEnvEnum).value}")

        manifestPlaceholders = [PACKAGE_NAME  : rootProject.ext.applicationId,
                                APP_NAME      : appNameStr,
                                UMENG_APPKEY  : rootProject.ext.uMengKey,
                                UMENG_CHANNEL : rootProject.ext.uMengChannel,
                                YX_KEY        : rootProject.ext.nimKey,
                                BAIDU_LOCATION: rootProject.ext.baidu]

        ndk {
            abiFilters "armeabi-v7a"
        }
    }




    signingConfigs {
        debug {
            storeFile file('../keystores/jmwh.jks')
            storePassword 'asdfghjkl'
            keyAlias 'jmwh'
            keyPassword 'asdfghjkl'
        }
        release {
            storeFile file('../keystores/jmwh.jks')
            storePassword 'asdfghjkl'
            keyAlias 'jmwh'
            keyPassword 'asdfghjkl'
            v2SigningEnabled true
        }
    }


    splits {
        abi {
            reset()
            universalApk false
            include "armeabi-v7a"
        }
    }

    productFlavors {
        jmwh {
            applicationId rootProject.ext.applicationId
            manifestPlaceholders = [PACKAGE_NAME  : rootProject.ext.applicationId,
                                    APP_NAME      : appNameStr,
                                    UMENG_APPKEY  : rootProject.ext.uMengKey,
                                    UMENG_CHANNEL : rootProject.ext.uMengChannel,
                                    YX_KEY        : rootProject.ext.nimKey,
                                    BAIDU_LOCATION: rootProject.ext.baidu]
        }
    }



    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFileAbsolutePath = output.outputFileName
            def isRelease = outputFileAbsolutePath.indexOf("release")
            outputFileName = isRelease >= 0 ? "app-release.apk" : "app-debug.apk"
        }

        variant.outputs.all { output ->
            def versionCodes = ["armeabi-v7a": 1]
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi != null) {
                output.versionCodeOverride = versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }

    buildTypes {

        def defaultProguardRule = 'proguard-rules.pro'
        def proguardBaseRule = '../proguard-rule/proguard-rules-base.pro'
        def proguardReacNativeRule = '../proguard-rule/proguard-rules-react-native-0.46.0.pro'
        def proguardRemoveLogRule = '../proguard-rule/proguard-rules-remove-logs.pro'
        def proguardThirdPartServiceRule = '../proguard-rule/proguard-rules-third-part-service.pro'
        def proguardUmengSharedRule = '../proguard-rule/proguard-rules-umeng-share.pro'

        debug {
            crunchPngs false //关闭crunchPng优化, 以加快构建
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }

        release {
            productFlavors.jmwh.signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile("proguard-android.txt"), defaultProguardRule,
                    proguardBaseRule, proguardReacNativeRule,
                    proguardRemoveLogRule,
                    proguardThirdPartServiceRule,
                    proguardUmengSharedRule
        }
    }

    compileOptions {
        //开启增量编译
        incremental = true
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    def enableCruncher = { ->
        return !isDebug()
    }
    //禁用cruncher, 以加速编译
    aaptOptions {
        cruncherEnabled = enableCruncher()
        cruncherProcesses = 0
    }

    //在Android代码块中添加如下配置：（可优化最上图中transformClassDexBuilderForDebug的时间）
    dexOptions {
        preDexLibraries true
        maxProcessCount
        dexInProcess true
        preDexLibraries true
        javaMaxHeapSize "2g"
    }

    //跳过Lint和Test相关的task, 以加速编译
    if (isDebug()) {
        gradle.taskGraph.whenReady {
            tasks.each { task ->
                if (task.name.contains("Test") || task.name.contains("Lint")) {
                    task.enabled = false
                }
            }
        }
    }


    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            jni.srcDirs = []
        }
    }

}



repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
//    implementation fileTree(include: ['*.jar'], dir: 'src/libs')
    implementation project(path: ':common')
    implementation project(path: ':http-lib')
    implementation project(path: ':bottom_tab')
    implementation project(':crop_img')
    implementation project(':share')
    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:${V7_APPCOMPAT_VERSION}"
    implementation "com.android.support.constraint:constraint-layout:${CONSTRAIT_SUPPORT_VERSION}"
    implementation "com.android.support:design:${DESIGIN_SUPPORT_VERSION}"
    implementation "com.android.support:recyclerview-v7:${V7_APPCOMPAT_VERSION}"
    //test
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.aliyun.dpa:oss-android-sdk:2.9.2'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    implementation 'ch.ielse:switchbutton:1.0.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation(name: 'alipaySdk-15.6.2-20190416165036', ext: 'aar')
    implementation project(path: ':location')
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-25'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-25'//没有使用特殊Header，可以不加这行
    implementation 'com.android.support:appcompat-v7:25.3.1'//版本 23以上（必须）

}
